plugins {
    id "groovy"
    id 'java-gradle-plugin'
    id "com.gradle.plugin-publish" version "0.10.1"
}

group = 'io.sdkman'
archivesBaseName = 'sdkman-vendor-gradle-plugin'

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation gradleApi()
    implementation 'org.codehaus.groovy:groovy-all:2.5.12'
    implementation 'org.hibernate:hibernate-validator:5.1.3.Final'
    implementation 'javax.el:javax.el-api:2.2.4'
    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'org.glassfish.web:javax.el:2.2.4'
    testImplementation 'org.spockframework.spock:spock-core:spock-1.3'
    testImplementation 'cglib:cglib-nodep:3.1'
    testImplementation 'com.github.tomakehurst:wiremock:2.26.3'
    testImplementation gradleTestKit()
}

pluginBundle {
    website = 'http://sdkman.io/'
    vcsUrl = 'https://github.com/sdkman/gradle-sdkman-vendor-plugin'
    description = 'The SDKMAN Vendor Gradle Plugin.'
    tags = ['sdkman', 'sdk', 'gvm', 'gvmtool']

    plugins {
        sdkmanVendorPlugin {
            id = 'io.sdkman.vendors'
            displayName = 'SDKMAN! Vendor Plugin'
        }
    }
}

apply plugin: 'distribution'

distributions {
    main {
        distributionBaseName = archivesBaseName
        contents {
            from { libsDir }
        }
    }
}

ext {
    bintrayBaseUrl = 'https://api.bintray.com/maven'
    bintrayUsername = 'sdkman'
    bintrayRepository = 'gradle-plugins'
    bintrayPackage = 'sdkman-vendor-gradle-plugin'
}

apply plugin: 'maven-publish'

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'source'
}

publishing {
    publications {
        plugin(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }

            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'SDKMAN! Vendor Gradle plugin')
                root.appendNode('description', 'Gradle plugin for SDKMAN! Vendors to publish Releases.')
                root.appendNode('inceptionYear', '2018')

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')

                def developer = root.appendNode('developers').appendNode('developer')
                developer.appendNode('name', 'Marco Vermeulen')
                developer.appendNode('email', 'marco@sdkman.io')
            }

        }
    }

    repositories {
        maven {
            name 'Bintray'
            url "$bintrayBaseUrl/$bintrayUsername/$bintrayRepository/$bintrayPackage"

            credentials {
                username = System.getenv('BINTRAY_USERNAME') ?: 'invalid_user'
                password = System.getenv('BINTRAY_API_KEY') ?: 'invalid_key'
            }
        }
    }
}
